@inline measure(aab::AABox2D) = Î”x(aab) * Î”y(aab)
@inline measure(aab::AABox3D) = Î”x(aab) * Î”y(aab) * Î”z(aab)
@inline measure(l::LineSegment) = distance(l.ğ˜…â‚.coord, l.ğ˜…â‚.coord + l.ğ˜‚)
function measure(q::QuadraticSegment)
    # The arc length integral may be reduced to an integral over the square root of a
    # quadratic polynomial using â€–ğ˜…â€– = âˆš(ğ˜… â‹… ğ˜…), which has an analytic solution.
    #     1             1
    # L = âˆ« â€–ğ—¾â€²(r)â€–dr = âˆ« âˆš(arÂ² + br + c) dr
    #     0             0
    if isstraight(q)
        return distance(q.ğ˜…â‚, q.ğ˜…â‚‚)
    else
        ğ˜‚ = q.ğ˜‚
        ğ˜ƒ = q.ğ˜ƒ
        a = 4(ğ˜‚ â‹… ğ˜‚)
        b = 4(ğ˜‚ â‹… ğ˜ƒ)
        c = ğ˜ƒ â‹… ğ˜ƒ
        # Compiler seems to catch the reused sqrt quantities for common subexpression
        # elimination, or computation is as quick as storage in a variable, so we
        # leave the sqrts for readability
        l = ((2a + b)âˆš(a + b + c) - bâˆšc)/4a -
            (b^2 - 4a*c)/((2âˆša)^3)*log((2âˆšaâˆš(a + b + c) + (2a + b))/(2âˆšaâˆšc + b))
        return l
    end
end

# Area
# ---------------------------------------------------------------------------------------------
# Uses the shoelace formula (https://en.wikipedia.org/wiki/Shoelace_formula)
function area(poly::Polygon{N, 2, T}) where {N, T}
    a = zero(T) # Scalar
    for i âˆˆ 1:N-1
        a += poly[i] Ã— poly[i + 1]
    end
    a += poly[N] Ã— poly[1]
    return norm(a)/2
end
# Not necessarily planar
#function area(poly::Polygon{N, 3, T}) where {N, T}
#    a = zero(SVector{3, T}) # Vector
#    for i âˆˆ 1:N-1
#        a += (poly[i] Ã— poly[i + 1])
#    end
#    a += poly[N] Ã— poly[1]
#    return norm(a)/2
#end
#

area(tri::Triangle2D) = norm((tri[2] - tri[1]) Ã— (tri[3] - tri[1]))/2
area(tri::Triangle3D) = norm((tri[2] - tri[1]) Ã— (tri[3] - tri[1]))/2

# Methods
# ---------------------------------------------------------------------------------------------
# area
#
# In general:
# Let ğ—³(r,s) be a parameterization of surface S
# A = âˆ¬ dS = âˆ¬ â€–âˆ‚ğ—³/âˆ‚r Ã— âˆ‚ğ—³/âˆ‚sâ€–dr ds
#     S      T
function area(poly::QuadraticPolygon{N,2,T}) where {N,T}
    # It can be shown that the area of the quadratic polygon is the area of the base
    # linear shape + the area filled by the quadratic curves outside/inside the linear
    # shape. The area of the quadratic edge is 4/3 the area of the triangle formed by the
    # 3 vertices.
    q = zero(T)
    l = zero(T)
    M = N Ã· 2
    for i âˆˆ 1:M-1
        q += poly[i    ] Ã— poly[i + M]
        q -= poly[i + 1] Ã— poly[i + M]
        l += poly[i] Ã— poly[i + 1]
    end
    q += poly[M] Ã— poly[N]
    q -= poly[1] Ã— poly[N]
    l += poly[M] Ã— poly[1]
    return (4q - l)/6
end

# The area integral for 3D quadratic triangles and quadrilaterals appears to have an
# analytic solution, but it involves finding the roots of a quartic polynomial, then 
# integrating over the square root of the factored quartic polynomial. 
# This has a solution in the form of elliptic integrals (See Byrd and Friedman's
# Handbook of Elliptic Integrals for Engineers and Scientists, 2nd edition, 
# equation 251.38), but it's absolutely massive. There may be simplifications after
# the fact that reduce the size of the expression, but for now numerical integration is 
# quicker.
function area(quad8::QuadraticQuadrilateral3D{T}, ::Val{N}) where {T, N}
    # Gauss-Legendre quadrature over a quadrilateral is used.
    # Let Q(r,s) be the interpolation function for quad8,
    #     1 1                          N   N
    # A = âˆ« âˆ« â€–âˆ‚Q/âˆ‚r Ã— âˆ‚Q/âˆ‚sâ€– ds dr =  âˆ‘   âˆ‘  wáµ¢wâ±¼â€–âˆ‚Q/âˆ‚r(ráµ¢,sâ±¼) Ã— âˆ‚Q/âˆ‚s(ráµ¢,sâ±¼)â€–
    #     0 0                         i=1 j=1
    w, r = gauss_legendre_quadrature(T, Val(N))
    A = zero(T)
    for j âˆˆ 1:N, i âˆˆ 1:N 
        J = ğ—(quad8, r[i], r[j]) 
        A += w[i]*w[j]*norm(view(J, :, 1) Ã— view(J, :, 2)) 
    end 
    return A
end
function area(tri6::QuadraticTriangle3D{T}, ::Val{N}) where {T, N} 
    # Gauss-Legendre quadrature over a triangle is used.
    # Let F(r,s) be the interpolation function for tri6,
    #            1 1-r                       N                
    # A = âˆ¬ dA = âˆ«  âˆ« â€–âˆ‚F/âˆ‚r Ã— âˆ‚F/âˆ‚sâ€–ds dr = âˆ‘ wáµ¢â€–âˆ‚F/âˆ‚r(ráµ¢,sáµ¢) Ã— âˆ‚F/âˆ‚s(ráµ¢,sáµ¢)â€–
    #     S      0  0                       i=1
    w, r, s = gauss_legendre_quadrature(tri6, Val(N))
    A = zero(T)
    for i âˆˆ 1:N
        J = ğ—(tri6, r[i], s[i])
        A += w[i] * norm(view(J, :, 1) Ã— view(J, :, 2)) 
    end
    return A
end

# Return the area of face id
function area(id, mesh::UnstructuredMesh)
    return area(materialize_face(id, mesh))
end

# Return the area of the entire mesh
function area(mesh::UnstructuredMesh)
    # use sum
    return mapreduce(x->area(x, mesh), +, 1:length(mesh.faces))
end

# Return the area of a face set
function area(face_set::BitSet, mesh::UnstructuredMesh)
    # use sum
    return mapreduce(x->area(x, mesh), +, face_set)
end

# Return the area of a face set by name
function area(set_name::String, mesh::UnstructuredMesh)
    return area(mesh.face_sets[set_name], mesh)
end
