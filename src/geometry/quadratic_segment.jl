export QuadraticSegment,
       QuadraticSegment2,
       QuadraticSegment2f,
       QuadraticSegment2d

export interpolate_quadratic_segment,
       jacobian,
       quadratic_segment_jacobian,
       arclength,
       area_enclosed_by,
       area_enclosed_by_quadratic_segment,
       centroid_of_area_enclosed_by,
       centroid_of_area_enclosed_by_quadratic_segment,
       bounding_box,
       isleft

# QUADRATIC SEGMENT
# -----------------------------------------------------------------------------
#
# A quadratic segment represented by 3 vertices.
# These vertices are D-dimensional points of type T.
#
# See chapter 8 of the VTK book for more info.
#
# It is helpful to know:
#  Q(r) = rÂ²ğ—® + ğ—¯r + C,
# where
#  ğ—® = 2(Pâ‚ + Pâ‚‚ - 2Pâ‚ƒ)
#  ğ—¯ = -3Pâ‚ - Pâ‚‚ + 4Pâ‚ƒ
#  C = Pâ‚

struct QuadraticSegment{D, T}
    vertices::NTuple{3, Point{D, T}}
end

# -- Type aliases --

const QuadraticSegment2  = QuadraticSegment{2}
const QuadraticSegment2f = QuadraticSegment2{Float32}
const QuadraticSegment2d = QuadraticSegment2{Float64}

# -- Base --

Base.getindex(Q::QuadraticSegment, i::Integer) = Q.vertices[i]

# -- Constructors --

function QuadraticSegment(P1::Point{D, T}, P2::Point{D, T}, P3::Point{D, T}) where {D, T}
    return QuadraticSegment{D, T}((P1, P2, P3))
end

# -- Interpolation --

function quadratic_segment_weights(r)
    return ((2 * r - 1) * (r - 1),
            (2 * r - 1) *  r     ,
            -4 * r      * (r - 1))
end

function interpolate_quadratic_segment(P1::T, P2::T, P3::T, r) where {T}
    w = quadratic_segment_weights(r)
    return w[1] * P1 + w[2] * P2 + w[3] * P3
end

function interpolate_quadratic_segment(vertices::NTuple{3}, r)
    return mapreduce(*, +, quadratic_segment_weights(r), vertices)
end

function (Q::QuadraticSegment{D, T})(r::T) where {D, T}
    return interpolate_quadratic_segment(Q.vertices, r)
end

# -- Jacobian --

function quadratic_segment_jacobian(P1::T, P2::T, P3::T, r) where {T}
    return (4 * r - 3) * (P1 - P3) +
           (4 * r - 1) * (P2 - P3)
end

function quadratic_segment_jacobian(vertices::NTuple{3}, r)
    return (4 * r - 3) * (vertices[1] - vertices[3]) +
           (4 * r - 1) * (vertices[2] - vertices[3])
end

function jacobian(Q::QuadraticSegment{D, T}, r::T) where {D, T}
    return quadratic_segment_jacobian(Q.vertices, r)
end

# -- Measure --

function arclength(Q::QuadraticSegment)
    # The arc length integral may be reduced to an integral over the square root of a
    # quadratic polynomial using â€–ğ˜…â€– = âˆš(ğ˜… â‹… ğ˜…), which has an analytic solution.
    #              1             1
    # arc length = âˆ« â€–Qâ€²(r)â€–dr = âˆ« âˆš(arÂ² + br + c) dr
    #              0             0
    #
    # If a = 0, we need to use a different formula, else the result is NaN.

    # Q(r) = rÂ²ğ—® + ğ—¯r + C,
    # where
    # ğ—® = 2(Pâ‚ + Pâ‚‚ - 2Pâ‚ƒ)
    # ğ—¯ = -3Pâ‚ - Pâ‚‚ + 4Pâ‚ƒ
    # C = Pâ‚
    # hence,
    # Q'(r) = 2ğ—®r + ğ—¯,
    ğ˜ƒâ‚â‚ƒ = Q[3] - Q[1]
    ğ˜ƒâ‚‚â‚ƒ = Q[3] - Q[2]
    ğ—® = -2(ğ˜ƒâ‚â‚ƒ + ğ˜ƒâ‚‚â‚ƒ)
    # Move computation of ğ—¯ to after exit.

    # â€–Qâ€²(r)â€– =  âˆš(4(ğ—® â‹…ğ—®)rÂ² + 4(ğ—® â‹…ğ—¯)r + ğ—¯ â‹…ğ—¯) = âˆš(arÂ² + br + c)
    # where
    # a = 4(ğ—® â‹… ğ—®)
    # b = 4(ğ—® â‹… ğ—¯)
    # c = ğ—¯ â‹… ğ—¯
    a = 4(ğ—® â‹… ğ—®)
    # 0 â‰¤ a, since a = 4(ğ—® â‹… ğ—®)  = 4 â€–ğ—®â€–Â², and 0 â‰¤ â€–ğ—®â€–Â²
    if a < 1e-5
        return distance(Q[1], Q[2])
    else

        ğ—¯ = 3ğ˜ƒâ‚â‚ƒ + ğ˜ƒâ‚‚â‚ƒ
        b = 4(ğ—® â‹… ğ—¯)
        c = ğ—¯ â‹… ğ—¯

        # âˆš(arÂ² + br + c) = âˆša âˆš( (r + bâ‚)^2 + câ‚)
        # where
        bâ‚ = b / (2 * a)
        câ‚ = (c / a) - bâ‚^2
        #
        # Let u = r + bâ‚, then
        # 1                       1 + bâ‚
        # âˆ« âˆš(arÂ² + br + c) dr = âˆša âˆ« âˆš(uÂ² + câ‚) du
        # 0                         bâ‚
        #
        # This is an integral that exists in common integral tables.
        # Evaluation of the resultant expression may be simplified by using
        lb = bâ‚
        ub = 1 + bâ‚
        L = âˆš(câ‚ + lb^2)
        U = âˆš(câ‚ + ub^2)

        return âˆša * (U + lb * (U - L) + câ‚ * ( atanh(ub / U) - atanh(lb / L) )) / 2
    end
end

# The area bounded by q and the line from Pâ‚ to Pâ‚‚ is 4/3 the area of the triangle
# formed by the vertices. Assumes the area is convex.
function area_enclosed_by(Q::QuadraticSegment2{T}) where {T}
    # Easily derived by transforming q such that Pâ‚ = (0, 0) and Pâ‚‚ = (xâ‚‚, 0).
    # However, vertices are CCW order, so sign of the area is flipped.
    return T(2 // 3) * ((Q[3] - Q[1]) Ã— (Q[2] - Q[1]))
end

function area_enclosed_by_quadratic_segment(
        P1::Point2{T}, P2::Point2{T}, P3::Point2{T}) where {T}
    return T(2 // 3) * ((P3 - P1) Ã— (P2 - P1))
end

# -- Centroid --

function centroid_of_area_enclosed_by(Q::QuadraticSegment2{T}) where {T}
    # For a quadratic segment, with Pâ‚ = (0, 0), Pâ‚‚ = (xâ‚‚, 0), and Pâ‚ƒ = (xâ‚ƒ, yâ‚ƒ),
    # where 0 < xâ‚‚, if the area bounded by q and the x-axis is convex, it can be
    # shown that the centroid of the area bounded by the segment and x-axis
    # is given by
    # C = (3xâ‚‚ + 4xâ‚ƒ, 4yâ‚ƒ) / 10
    #
    # To find the centroid of the area bounded by the segment for a general
    # quadratic segment, we transform the segment so that Pâ‚ = (0, 0),
    # then use a change of basis (rotation) from the standard basis to the
    # following basis, to achieve yâ‚‚ = 0.
    #
    # Let v = (vâ‚, vâ‚‚) = (Pâ‚‚ - Pâ‚) / â€–Pâ‚‚ - Pâ‚â€–
    # uâ‚ = ( vâ‚,  vâ‚‚) = v
    # uâ‚‚ = (-vâ‚‚,  vâ‚)
    #
    # Note: uâ‚ and uâ‚‚ are orthonormal.
    #
    # The transformation from the new basis to the standard basis is given by
    # U = [uâ‚ uâ‚‚] = | vâ‚ -vâ‚‚ |
    #               | vâ‚‚  vâ‚ |
    #
    # Since uâ‚ and uâ‚‚ are orthonormal, U is unitary.
    #
    # The transformation from the standard basis to the new basis is given by
    # Uâ»Â¹ = Uáµ— = |  vâ‚  vâ‚‚ |
    #            | -vâ‚‚  vâ‚ |
    # since U is unitary.
    #
    # Therefore, the centroid of the area bounded by the segment is given by
    # C = U * Cáµ¤ + Pâ‚
    # where
    # Cáµ¤ = (uâ‚ â‹… (3(Pâ‚‚ - Pâ‚) + 4(Pâ‚ƒ - Pâ‚)), 4(uâ‚‚ â‹… (Pâ‚ƒ - Pâ‚))) / 10
    vâ‚â‚‚ = Q[2] - Q[1]
    four_vâ‚â‚ƒ = 4*(Q[3] - Q[1])
    uâ‚ = normalize(vâ‚â‚‚)
    uâ‚‚ = Vec(-uâ‚[2], uâ‚[1])
    U  = Mat(uâ‚, uâ‚‚)
    Cáµ¤ = Vec(uâ‚ â‹…(3 * vâ‚â‚‚ + four_vâ‚â‚ƒ), uâ‚‚ â‹… four_vâ‚â‚ƒ) / 10
    return U * Cáµ¤ + Q[1]
end

function centroid_of_area_enclosed_by_quadratic_segment(
        P1::P, P2::P, P3::P) where {P <: Point2}
    vâ‚â‚‚ = P2 - P1
    four_vâ‚â‚ƒ = 4*(P3 - P1)
    uâ‚ = normalize(vâ‚â‚‚)
    uâ‚‚ = Vec(-uâ‚[2], uâ‚[1])
    U  = Mat(uâ‚, uâ‚‚)
    Cáµ¤ = Vec(uâ‚ â‹…(3 * vâ‚â‚‚ + four_vâ‚â‚ƒ), uâ‚‚ â‹… four_vâ‚â‚ƒ) / 10
    return U * Cáµ¤ + P1
end

# -- Bounding box --

function bounding_box(Q::QuadraticSegment2{T}) where {T}
    # Find the extrema for x and y by finding:
    # r_x such that dx/dr = 0    
    # r_y such that dy/dr = 0    
    # Q(r) = rÂ²ğ—® + ğ—¯r + C
    # Qâ€²(r) = 2ğ—®r + ğ—¯ 
    # (r_x, r_y) = -ğ—¯ ./ (2ğ—®)    
    # Compare the extrema with the segment's endpoints to find the AABox    
    P1 = Q[1]
    P2 = Q[2]
    P3 = Q[3]
    ğ˜ƒâ‚â‚ƒ = P3 - P1
    ğ˜ƒâ‚‚â‚ƒ = P3 - P2
    a_x, a_y = -2(ğ˜ƒâ‚â‚ƒ + ğ˜ƒâ‚‚â‚ƒ)
    b_x, b_y = 3ğ˜ƒâ‚â‚ƒ + ğ˜ƒâ‚‚â‚ƒ
    r_x = b_x / (-2 * a_x)
    r_y = b_y / (-2 * a_y)
    xmin = min(P1[1], P2[1]); ymin = min(P1[2], P2[2])
    xmax = max(P1[1], P2[1]); ymax = max(P1[2], P2[2])
    if 0 < r_x < 1
        x_stationary = r_x * r_x * a_x + r_x * b_x + P1[1]
        xmin = min(xmin, x_stationary)
        xmax = max(xmax, x_stationary)
    end
    if 0 < r_y < 1
        y_stationary = r_y * r_y * a_y + r_y * b_y + P1[2]
        ymin = min(ymin, y_stationary)
        ymax = max(ymax, y_stationary)
    end
    return AABox(Point2(xmin, ymin), Point2(xmax, ymax))
end

# -- In --

function isleft(P::Point2{T}, Q::QuadraticSegment2{T}) where {T}
    # If the point is in the bounding box of the segment,
    # we need to check if the point is left of the segment.
    # To do this we must find the point on q that is closest to P.
    # At this Q(r) we compute Q'(r) Ã— (P - Q(r)). If this quantity is
    # positive, then P is left of the segment.
    #
    # To compute Q_nearest, we find r which minimizes â€–P - Q(r)â€–.
    # This r also minimizes â€–P - Q(r)â€–Â².
    # It can be shown that this is equivalent to finding the minimum of the 
    # quartic function
    # â€–P - Q(r)â€–Â² = f(r) = aâ‚„râ´ + aâ‚ƒrÂ³ + aâ‚‚rÂ² + aâ‚r + aâ‚€
    # The minimum of f(r) occurs when fâ€²(r) = arÂ³ + brÂ² + cr + d = 0, where
    # ğ˜„ = P - Pâ‚
    # a = 4(ğ—® â‹… ğ—®)
    # b = 6(ğ—® â‹… ğ—¯)
    # c = 2[(ğ—¯  â‹… ğ—¯) - 2(ğ—® â‹…ğ˜„)]
    # d = -2(ğ—¯ â‹… ğ˜„)
    # Lagrange's method is used to find the roots.
    # (https://en.wikipedia.org/wiki/Cubic_equation#Lagrange's_method)    
    ğ˜ƒâ‚â‚ƒ = Q[3] - Q[1]
    ğ˜ƒâ‚‚â‚ƒ = Q[3] - Q[2]
    ğ—® = -2(ğ˜ƒâ‚â‚ƒ + ğ˜ƒâ‚‚â‚ƒ)    
    a = 4 * (ğ—® â‹… ğ—®)

    if a < 1e-5 # quadratic is straight
        return 0 â‰¤ (Q[2] - Q[1]) Ã— (P - Q[1])
    end

    ğ—¯ = 3ğ˜ƒâ‚â‚ƒ + ğ˜ƒâ‚‚â‚ƒ
    ğ˜„ = P - Q[1]

    b = 6 * (ğ—® â‹… ğ—¯)
    c = 2 * ((ğ—¯  â‹… ğ—¯) - 2 * (ğ—® â‹…ğ˜„))
    d = -2 * (ğ—¯ â‹… ğ˜„)

    # Lagrange's method
    eâ‚ = sâ‚€ = -b / a
    eâ‚‚ = c / a
    eâ‚ƒ = -d / a
    A = 2eâ‚^3 - 9eâ‚ * eâ‚‚ + 27eâ‚ƒ
    B = eâ‚^2 - 3eâ‚‚
    disc = A^2 - 4B^3
    if 0 < disc # one real root
        sâ‚ = âˆ›((A + âˆš(disc)) / 2)
        if sâ‚ == 0
            sâ‚‚ = sâ‚
        else
            sâ‚‚ = B / sâ‚
        end
        r = (sâ‚€ + sâ‚ + sâ‚‚) / 3
        return 0 â‰¤ jacobian(Q, r) Ã— (P - Q(r))
    else # three real roots
        # tâ‚ is complex cube root
        tâ‚ = exp(log((A + âˆš(complex(disc))) / 2) / 3)
        if tâ‚ == 0
            tâ‚‚ = tâ‚
        else
            tâ‚‚ = B / tâ‚
        end
        Î¶â‚ = Complex{T}(-1 / 2, âˆš3 / 2)
        Î¶â‚‚ = conj(Î¶â‚)

        # Pick the point closest to P
        r = T(0)
        d = T(INF_POINT) 

        r1 = real((sâ‚€ + tâ‚ + tâ‚‚)) / 3
        if 0 < r1 < 1
            d1 = distance2(P, Q(r1))
            if d1 < d
                r = r1
                d = d1
            end
        end

        r2 = real((sâ‚€ + Î¶â‚‚ * tâ‚ + Î¶â‚ * tâ‚‚)) / 3
        if 0 < r2 < 1
            d2 = distance2(P, Q(r2))
            if d2 < d
                r = r2
                d = d2
            end
        end

        r3 = real((sâ‚€ + Î¶â‚ * tâ‚ + Î¶â‚‚ * tâ‚‚)) / 3
        if 0 < r3 < 1
            d3 = distance2(P, Q(r3))
            if d3 < d
                r = r3
            end
        end

        return 0 â‰¤ jacobian(Q, r) Ã— (P - Q(r))
    end
end

# -- IO --

function Base.show(io::IO, Q::QuadraticSegment{D, T}) where {D, T}
    type_char = '?'
    if T === Float32
        type_char = 'f'
    elseif T === Float64
        type_char = 'd'
    end
    print(io, "QuadraticSegment", D, type_char, '(',
        Q.vertices[1], ", ",
        Q.vertices[2], ", ",
        Q.vertices[3], ')')
end
